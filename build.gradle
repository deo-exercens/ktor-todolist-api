buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'io.gitlab.arturbosch.detekt'

group 'com.exercens.deo'
version '0.0.1-SNAPSHOT'
mainClassName = "io.ktor.server.cio.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
}
dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-cio:$ktor_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    testCompile "io.ktor:ktor-server-tests:$ktor_version"
}

test {
    jacoco {
        destinationFile = file("${buildDir}/jacoco/test.exec")
    }
}
test.finalizedBy(jacocoTestReport)

// test code coverage
jacoco {
    // You may modify the Jacoco version here
    toolVersion = "0.8.4"
}

// test code coverage report
jacocoTestReport {
    // Adjust the output of the test report
    reports {
        xml.enabled true
        csv.enabled false
    }
}

// 정적 분석
detekt {
    toolVersion = "$detekt_version"
    input = files("src")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("config/detekt/detekt.yml")
    reports {
        xml {
            enabled = true
            destination = file("${buildDir}/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("${buildDir}/detekt/detekt.html")
        }
        txt {
            enabled = true
            destination = file("${buildDir}/detekt/detekt.txt")
        }
    }
}